configfile: "config/config.yaml"


pepfile: config["pepfile"]


container: "docker://condaforge/mambaforge"


include: "rules/common.smk"
include: "rules/utils.smk"
include: "rules/ref.smk"
include: "rules/qc.smk"
include: "rules/read_trimming.smk"
include: "rules/assembly.smk"
include: "rules/strain_calling.smk"
include: "rules/read_mapping.smk"
include: "rules/variant_calling.smk"
include: "rules/variant_annotation.smk"
include: "rules/variant_filtration.smk"
include: "rules/variant_report.smk"
include: "rules/benchmarking.smk"
include: "rules/generate_output.smk"


rule all:
    input:
        expand(
            "results/{date}/qc/multiqc.html", date=get_all_run_dates(),
        ),
        expand("results/rki/{date}_uk-essen_rki.fasta", date=get_all_run_dates()),
        expand(
            "results/{date}/plots/strain-calls/all.strains.pangolin.svg",
            date=get_all_run_dates(),
        ),
        expand(
            "results/{date}/plots/strain-calls/all.{mode}-strain.strains.kallisto.svg",
            date=get_all_run_dates(),
            mode=["major", "any"],
        ),
        expand(
            "results/{date}/polished-contigs/{sample}.fasta",
            zip,
            date=get_dates(),
            sample=get_samples(),
        ),
        expand(
            "results/{date}/tables/strain-calls/{sample}.strains.pangolin.csv",
            zip,
            date=get_dates(),
            sample=get_samples(),
        ),
        zip_expand(
            "results/{zip1}/plots/strain-calls/{zip2}.strains.{{exp}}.svg",
            zip_wildcard_1=get_dates(),
            zip_wildcard_2=get_samples(),
            expand_wildcard=["kallisto", "pangolin"],
        ),
        zip_expand(
            "results/{zip1}/filtered-calls/ref~main/{zip2}.subclonal.{{exp}}.bcf",
            zip_wildcard_1=get_dates(),
            zip_wildcard_2=get_samples(),
            expand_wildcard=config["variant-calling"]["filters"],
        ),
        zip_expand(
            "results/{zip1}/vcf-report/{zip2}.{{exp}}",
            zip_wildcard_1=get_dates() + get_all_run_dates(),
            zip_wildcard_2=get_samples() + ["all"] * len(get_all_run_dates()),
            expand_wildcard=config["variant-calling"]["filters"],
        ),
        expand(
            "results/{date}/virologist/report.csv", date=get_all_run_dates(),
        ),
        expand(
            "results/{date}/var_data/", date=get_all_run_dates(),
        ),
        expand(
            "results/{date}/qc_data/", date=get_all_run_dates(),
        ),


rule benchmark_strain_calling:
    input:
        "results/benchmarking/strain-calling.csv",


rule benchmark_assembly:
    input:
        "results/benchmarking/assembly.csv",
